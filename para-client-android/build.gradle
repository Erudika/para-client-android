apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def gitURL = 'git@github.com:Erudika/para-client-android.git'
def paraVer = '1.18.3'
group = "com.erudika"
version = paraVer

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    useLibrary 'org.apache.http.legacy'
    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName paraVer
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/INDEX.LIST'
    }
    dexOptions {
        incremental true
        javaMaxHeapSize "4g"
    }
    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }
    allprojects {
        gradle.projectsEvaluated {
            tasks.withType(JavaCompile) {
                options.compilerArgs << "-Xlint:unchecked"
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:support-v4:+'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.mockito:mockito-core:1.10.8'
    compile 'com.fasterxml.jackson.core:jackson-core:2.7.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.0'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.7.0'
    compile 'org.slf4j:jcl-over-slf4j:1.7.13'
    compile 'org.slf4j:slf4j-api:1.7.13'
    compile 'org.slf4j:jul-to-slf4j:1.7.13'
    compile 'org.slf4j:log4j-over-slf4j:1.7.13'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.noveogroup.android:android-logger:1.3.6'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.amazonaws:aws-java-sdk-core:1.10.49'
    // DO NOT USE Android SDK - breaks tests because of PATCH methods
    //compile 'com.amazonaws:aws-android-sdk-core:2.2.10'
}

configurations {
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    all*.exclude group: 'org.apache.httpcomponents', module: 'httpclient'
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'para-client-android'
                description = 'Android client library for Para'
                url 'http://www.paraio.org'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'albogdano'
                        name 'Alex Bogdanovski'
                        email 'alex@erudika.com'
                    }
                }
                scm {
                    connection gitURL
                    developerConnection gitURL
                    url 'https://github.com/Erudika/para-client-android'
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    into('libs') {
        from configurations.compile
    }
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError = false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

allprojects {
    bintray {
        user = properties.getProperty("bintray.user")
        key = properties.getProperty("bintray.apikey")

        configurations = ['archives']
        pkg {
            userOrg = "erudika"
            repo = "releases"
            name = "para-client-android"
            websiteUrl = "http://www.paraio.org"
            issueTrackerUrl = 'https://github.com/Erudika/para-client-android/issues'
            githubRepo = 'Erudika/para-client-android'
            githubReleaseNotesFile = 'README.md'
            vcsUrl = gitURL
            licenses = ["Apache-2.0"]
            labels = ['para', 'backend', 'server', 'api', 'erudika', 'sdk']
            publish = true
            version {
                name = paraVer
                released = new Date()
                vcsTag = paraVer
            }
        }
    }
}


