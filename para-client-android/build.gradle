apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def gitURL = 'git@github.com:Erudika/para-client-android.git'
def paraVer = '1.33.1'
group = "com.erudika"
version = paraVer

android {
    compileSdkVersion 30
//    buildToolsVersion "28.0.3"
    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        minSdkVersion 26
        //targetSdkVersion 26
        versionCode 1
        versionName paraVer
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/INDEX.LIST'
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }
    buildToolsVersion '30.0.3'
    allprojects {
        gradle.projectsEvaluated {
            tasks.withType(JavaCompile) {
                options.compilerArgs << "-Xlint:deprecation"
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //implementation 'com.android.support:appcompat-v7:26.1.0'
    //implementation 'com.android.support:support-v4:26.1.0'
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'org.mockito:mockito-core:2.28.2'
    androidTestImplementation 'com.android.support:support-annotations:24.2.1'
    androidTestImplementation 'com.android.support.test:runner:0.5'
    androidTestImplementation 'com.android.support.test:rules:0.5'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.9'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.9.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.9'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.9.9'
    implementation 'org.slf4j:jcl-over-slf4j:1.7.26'
    implementation 'org.slf4j:slf4j-api:1.7.26'
    implementation 'org.slf4j:jul-to-slf4j:1.7.26'
    implementation 'org.slf4j:log4j-over-slf4j:1.7.26'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'com.noveogroup.android:android-logger:1.3.6'
    implementation 'com.android.volley:volley:1.1.0'
    implementation 'com.squareup.okio:okio:1.17.4'
    implementation 'com.squareup.okhttp3:okhttp:3.14.2'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:3.14.2'
    implementation 'com.amazonaws:aws-android-sdk-core:2.13.6'
}

configurations {
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    all*.exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    all*.exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-cbor'
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'para-client-android'
                description = 'Android client library for Para'
                url 'https://paraio.org'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'albogdano'
                        name 'Alex Bogdanovski'
                        email 'alex@erudika.com'
                    }
                }
                scm {
                    connection gitURL
                    developerConnection gitURL
                    url 'https://github.com/Erudika/para-client-android'
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    into('libs') {
        from configurations.compile
    }
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

//task javadoc(type: Javadoc) {
//    failOnError = false
//    source = android.sourceSets.main.java.srcDirs
//    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
//}

//afterEvaluate {
//    javadoc.classpath += files(android.libraryVariants.collect { variant ->
//        variant.javaCompileProvider.get().classpath.files
//    })
//}
//
//
//task javadocJar(type: Jar, dependsOn: javadoc) {
//    classifier = 'javadoc'
//    from javadoc.destinationDir
//}

artifacts {
//    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

allprojects {
    bintray {
        user = properties.getProperty("bintray.user")
        key = properties.getProperty("bintray.apikey")

        configurations = ['archives']
        pkg {
            userOrg = "erudika"
            repo = "releases"
            name = "para-client-android"
            websiteUrl = "https://paraio.org"
            issueTrackerUrl = 'https://github.com/Erudika/para-client-android/issues'
            githubRepo = 'Erudika/para-client-android'
            githubReleaseNotesFile = 'README.md'
            vcsUrl = gitURL
            licenses = ["Apache-2.0"]
            labels = ['para', 'backend', 'server', 'api', 'erudika', 'sdk']
            publish = true
            version {
                name = paraVer
                //released = new Date()
                vcsTag = paraVer
            }
        }
    }
}


