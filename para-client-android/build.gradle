plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

def gitURL = 'https://github.com/Erudika/para-client-android.git'
def paraVer = '1.33.1'
group = "com.erudika"
version = paraVer

android {
    namespace = "com.erudika.para.client"
    compileSdk = 34

    defaultConfig {
        minSdk = 26
        targetSdk = 34
        versionCode = 1
        versionName =  paraVer
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        consumerProguardFiles 'proguard-rules.pro'
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    buildTypes {
        release {
            minifyEnabled = true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packaging {
        resources {
            excludes += [
                    'META-INF/NOTICE',
                    'META-INF/NOTICE.txt',
                    'META-INF/LICENSE',
                    'META-INF/LICENSE.txt',
                    'META-INF/INDEX.LIST'
            ]
        }
    }

    lint {
        disable += ['InvalidPackage']
        abortOnError = false
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'org.mockito:mockito-android:5.12.0'

    // JSON
    implementation platform('com.fasterxml.jackson:jackson-bom:2.17.2')
    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.core:jackson-annotations'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor'

    // HTTP & networking
    implementation 'com.android.volley:volley:1.2.1'
    implementation platform('com.squareup.okhttp3:okhttp-bom:4.12.0')
    implementation 'com.squareup.okhttp3:okhttp'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection'

    // Logging and utils
    implementation 'org.slf4j:jcl-over-slf4j:2.0.13'
    implementation 'org.slf4j:slf4j-api:2.0.13'
    implementation 'org.slf4j:jul-to-slf4j:2.0.13'
    implementation 'org.slf4j:log4j-over-slf4j:2.0.13'
    implementation 'org.apache.commons:commons-lang3:3.14.0'

    // AWS v4 Signatures
    implementation 'com.github.davidmoten:aws-lightweight-client-java:0.1.24'
}

// configurations.configureEach {
//     exclude 'commons-logging:commons-logging'
//     exclude 'org.apache.httpcomponents:httpclient'
//     exclude 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor'
// }

publishing {
    publications {
        register('release', MavenPublication) {
            groupId = project.group
            artifactId = 'para-client-android'
            version = project.version

            pom {
                packaging = 'aar'
                name = 'para-client-android'
                description = 'Android client library for Para'
                url = 'https://paraio.org'
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'albogdano'
                        name = 'Alex Bogdanovski'
                        email = 'alex@erudika.com'
                    }
                }
                scm {
                    connection = gitURL
                    developerConnection = gitURL
                    url = 'https://github.com/Erudika/para-client-android'
                }
            }

            // afterEvaluate {
            //     from components.release
            // }
        }
    }

    repositories {
        // def publishUrl = providers.gradleProperty("erudika.publishUrl")
        //         .orElse(providers.environmentVariable("ERUDIKA_PUBLISH_URL"))
        maven {
            name = "central"
            url = uri("https://repo.maven.apache.org/maven2/")
            credentials {
                username = providers.gradleProperty("erudika.publishUser")
                        .orElse(providers.environmentVariable("MAVEN_PUBLISH_USER"))
                        .orNull
                password = providers.gradleProperty("erudika.publishPassword")
                        .orElse(providers.environmentVariable("MAVEN_PUBLISH_PASS"))
                        .orNull
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications
    // def signingKey = providers.gradleProperty("erudika.signingKey")
    //         .orElse(providers.environmentVariable("ERUDIKA_SIGNING_KEY"))
    // def signingPassword = providers.gradleProperty("erudika.signingPassword")
    //         .orElse(providers.environmentVariable("ERUDIKA_SIGNING_PASSWORD"))
    // if (signingKey.isPresent() && signingPassword.isPresent()) {
    //     useInMemoryPgpKeys(signingKey.get(), signingPassword.get())
    //     sign publishing.publications
    // }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:deprecation"
}
