apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def gitURL = 'git@github.com:Erudika/para-client-android.git'
def paraVer = '1.21.0'
group = "com.erudika"
version = paraVer

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"
    useLibrary 'org.apache.http.legacy'
    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName paraVer
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/INDEX.LIST'
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }
    allprojects {
        gradle.projectsEvaluated {
            tasks.withType(JavaCompile) {
                options.compilerArgs << "-Xlint:unchecked"
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:support-v4:24.2.1'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.mockito:mockito-core:2.2.0'
    androidTestCompile 'com.android.support:support-annotations:24.2.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    compile 'com.fasterxml.jackson.core:jackson-core:2.8.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.3'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.8.3'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.7.4'
    compile 'org.slf4j:jcl-over-slf4j:1.7.21'
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'org.slf4j:jul-to-slf4j:1.7.21'
    compile 'org.slf4j:log4j-over-slf4j:1.7.21'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.noveogroup.android:android-logger:1.3.6'
    compile 'com.squareup.okio:okio:1.10.0'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.4.1'
    compile 'com.amazonaws:aws-android-sdk-core:2.3.2'
}

configurations {
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    all*.exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    all*.exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-cbor'
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'para-client-android'
                description = 'Android client library for Para'
                url 'https://paraio.org'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'albogdano'
                        name 'Alex Bogdanovski'
                        email 'alex@erudika.com'
                    }
                }
                scm {
                    connection gitURL
                    developerConnection gitURL
                    url 'https://github.com/Erudika/para-client-android'
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    into('libs') {
        from configurations.compile
    }
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError = false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

allprojects {
    bintray {
        user = properties.getProperty("bintray.user")
        key = properties.getProperty("bintray.apikey")

        configurations = ['archives']
        pkg {
            userOrg = "erudika"
            repo = "releases"
            name = "para-client-android"
            websiteUrl = "https://paraio.org"
            issueTrackerUrl = 'https://github.com/Erudika/para-client-android/issues'
            githubRepo = 'Erudika/para-client-android'
            githubReleaseNotesFile = 'README.md'
            vcsUrl = gitURL
            licenses = ["Apache-2.0"]
            labels = ['para', 'backend', 'server', 'api', 'erudika', 'sdk']
            publish = true
            version {
                name = paraVer
                released = new Date()
                vcsTag = paraVer
            }
        }
    }
}


